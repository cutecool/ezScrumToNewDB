*** Settings ***
Library           ../lib/Selenium2Improved.py
Resource          ../../Global Define.txt
Resource          ../common_resource.txt

*** Keywords ***
Edit Story With Arguments In Sprint Backlog
    [Arguments]    ${oriTargetStoryName}    ${storyName}    ${storyValue}    ${storyEstimate}    ${storyImportance}    ${storyNotes}
    ...    ${storyHowToDemo}
    Click Element    xpath=//div[@class="x-treegrid-text" and (text()="${oriTargetStoryName}")]/../..
    Wait Until Element Is Visible    xpath=//button[text()="Edit Story"]
    Click Element    xpath=//button[text()="Edit Story"]
    Focus AND Input Text    //textarea[@name="Name"]    ${storyName}
    Focus AND Input Text    //input[@name="Value"]    ${storyValue}
    Focus AND Input Text    //input[@name="Estimate"]    ${storyEstimate}
    Focus AND Input Text    //input[@name="Importance"]    ${storyImportance}
    Focus AND Input Text    //textarea[@name="Notes"]    ${storyNotes}
    Focus AND Input Text    //textarea[@name="HowToDemo"]    ${storyHowToDemo}
    ${storyID}=    Get Value    xpath=//input[@name="issueID"]
    ${xpathEditStory}=    Find Current Window Element    Edit Story #${storyID}    Submit
    Element Enable And Submit    ${xpathEditStory}
    Wait Until Page Contains    Success.
    [Return]    ${storyID}

Verify Story Information With Arguments In Sprint Backlog
    [Arguments]    ${storyName}    ${storyValue}    ${storyEstimate}    ${storyImportance}    ${tagName}    ${storyNotes}
    ...    ${storyHowToDemo}
    # 比對 edit story widget content
    Double Click Element    xpath=//div[@class="x-treegrid-text" and (text()="${storyName}")]/../..
    Compare Field Content    //textarea[@name="Name"]    ${storyName}
    Compare Field Content    //input[@name="Value"]    ${storyValue}
    Compare Field Content    //input[@name="Estimate"]    ${storyEstimate}
    Compare Field Content    //input[@name="Importance"]    ${storyImportance}
    Compare Field Content    //textarea[@name="Notes"]    ${storyNotes}
    Compare Field Content    //input[@name="Tags"]    ${tagName}
    Compare Field Content    //textarea[@name="HowToDemo"]    ${storyHowToDemo}
    ${xpathAddNewSprint}=    Find Current Window Element    Edit Story #1    Cancel
    Element Enable And Submit    ${xpathAddNewSprint}

Get Story ID In Sprint Backlog
    [Arguments]    ${stroyName}
    Sleep    5s
    Wait Until Page Contains Element    xpath=//div[@class="x-treegrid-text" and (text()="${stroyName}")]/../../td[1]/a/span
    ${storyID}=    Get Text    xpath=//div[@class="x-treegrid-text" and (text()="${stroyName}")]/../../td[1]/a/span
    [Return]    ${storyID}

Drop Story
    [Arguments]    ${oriTargetStoryName}
    Wait Until Page Contains Element    xpath=//div[@class="x-treegrid-text" and (text()="${oriTargetStoryName}")]/../..    # 同步機制
    Click Element    xpath=//div[@class="x-treegrid-text" and (text()="${oriTargetStoryName}")]/../..
    Click Element    xpath=//button[text()="Drop Story"]
    ${xpathDropStory}=    Find Current Window Element    Drop Story    Drop
    Element Enable And Submit    ${xpathDropStory}

Add Existing Story
    [Arguments]    ${storyName}
    # click show add existing story window
    Select Node Product    Sprint Backlog
    Click Element    xpath=//button[text()="Add Existing Stories"]
    Wait Until Page Contains    Add Existing Stories
    Mouse Down    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-4" and (text()="${storyName}")]
    Mouse Up    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-4" and (text()="${storyName}")]
    Click Element    xpath=//button[text()="Add Existed Stories"]
    Wait Until Page Contains    Add Existing Stories
